; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		unary.asm
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		5th January 2019
;		Purpose :	Unary operators (A ? B -> A)
;
; ***************************************************************************************
; ***************************************************************************************

@xmacro 	--
		dec 	hl
@end

; ***************************************************************************************

@xmacro		---
		dec 	hl
		dec 	hl
@end

; ***************************************************************************************

@xmacro 	++
		inc 	hl
@end

; ***************************************************************************************

@xmacro		+++
		inc 	hl
		inc 	hl
@end

; ***************************************************************************************

@word 	0-
__negate:
		ld 		a,h
		cpl 
		ld 		h,a
		ld 		a,l
		cpl
		ld 		l,a
		inc 	hl
		ret
@end

; ***************************************************************************************

@word 	0<
		bit 	7,h
		ld 		hl,$0000
		ret 	z
		dec 	hl
		ret
@end

; ***************************************************************************************

@word 	0=
		ld 		a,h
		or 		l
		ld 		hl,$0000
		ret 	nz
		dec 	hl
		ret
@end

; ***************************************************************************************

@xmacro	2* 
		add 	hl,hl
@end

@xmacro	4* 
		add 	hl,hl
		add 	hl,hl
@end

@xmacro	8* 
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
@end

@xmacro	16*
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
@end

; ***************************************************************************************

@xmacro	2/ 
		sra 	h
		rr 		l
@end

@xmacro	4/
		sra 	h
		rr 		l
		sra 	h
		rr 		l
@end

; ***************************************************************************************

@word 	abs
		bit 	7,h
		ret		z
		jp 		__negate
@end

; ***************************************************************************************

@xmacro	bswap	
		ld 		a,l
		ld 		l,h
		ld 		h,a
@end

; ***************************************************************************************

@word 	not
		ld 		a,h
		cpl 
		ld 		h,a
		ld 		a,l
		cpl
		ld 		l,a
		ret
@end
