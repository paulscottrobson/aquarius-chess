; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		unary.def
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		14th October 2021
;		Purpose :	Unary operators (A ? B -> A)
;
; ***************************************************************************************
; ***************************************************************************************

@copies		---
		dec 	hl
		dec 	hl
@end

; ***************************************************************************************

@copies 	--
		dec 	hl
@end

; ***************************************************************************************

@copies 	++
		inc 	hl
@end

; ***************************************************************************************

@copies		+++
		inc 	hl
		inc 	hl
@end

; ***************************************************************************************

@calls 	0-
__negate:
		ld 		a,h
		cpl 
		ld 		h,a
		ld 		a,l
		cpl
		ld 		l,a
		inc 	hl
		ret
@end

; ***************************************************************************************

@calls 	0<
		bit 	7,h
		ld 		hl,$0000
		ret 	z
		dec 	hl
		ret
@end

; ***************************************************************************************

@calls 	0=
		ld 		a,h
		or 		l
		ld 		hl,$0000
		ret 	nz
		dec 	hl
		ret
@end

; ***************************************************************************************

@copies	2* 
		add 	hl,hl
@end

@copies	4* 
		add 	hl,hl
		add 	hl,hl
@end

@copies	8* 
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
@end

@copies	16*
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
		add 	hl,hl
@end

; ***************************************************************************************

@copies	2/ 
		sra 	h
		rr 		l
@end

@copies	4/
		sra 	h
		rr 		l
		sra 	h
		rr 		l
@end

; ***************************************************************************************

@calls 	abs
		bit 	7,h
		ret		z
		jp 		__negate
@end

; ***************************************************************************************

@copies	bswap	
		ld 		a,l
		ld 		l,h
		ld 		h,a
@end

; ***************************************************************************************

@calls 	not
		ld 		a,h
		cpl 
		ld 		h,a
		ld 		a,l
		cpl
		ld 		l,a
		ret
@end

; ***************************************************************************************

@calls strlen
		push 	de
		ex 		de,hl
		ld 		hl,0
_SLNLoop:
		ld 		a,(de)
		or 		a
		jr 		z,_SLNExit
		inc 	de
		inc 	hl
		jr 		_SLNLoop
_SLNExit:
		pop 	de
		ret
@end 
				
; ***************************************************************************************

@calls 	random
	ex 		de,hl
	push 	bc
    ld 		hl,(seed1)
    ld 		b,h
    ld 		c,l
    add 	hl,hl
    add 	hl,hl
    inc 	l
    add 	hl,bc
    ld 		(seed1),hl
    ld 		hl,(seed2)
    add 	hl,hl
    sbc 	a,a
    and 	%00101101
    xor 	l
    ld 		l,a
    ld 		(seed2),hl
    add 	hl,bc
    pop 	bc
    ret
