; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		system.def
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		16th October 2021
;		Purpose :	System things
;
; ***************************************************************************************
; ***************************************************************************************

; ***************************************************************************************
;
; 		H gives the address of the code pointer (for the value you do h @)
;
; ***************************************************************************************

@copies h 
		ex  de,hl
		ld 	hl,CodeNextFree
@end

; ***************************************************************************************
;
; 					$SYSTEM points to the information block 
;
; ***************************************************************************************

@copies $system
		ex  de,hl
		ld 	hl,InformationBlock
@end

; ***************************************************************************************
;
;							   Get dictionary base
;
; ***************************************************************************************

@copies $dictionary
		ex 	de,hl
		ld 	hl,(DictionaryBase)
@end

; ***************************************************************************************
;
;								Interface entry points
;
; ***************************************************************************************

@calls warm.start
		jp 		WarmStart
@end

@calls report.error
		jp 		Interface
@end 

@calls $compiler
		jp 		CompileStream
@end 

; ***************************************************************************************
;
;							Word that defines a constant
;
; ***************************************************************************************

@executes constant
		ld 		hl,(CodeNextFree) 			; fix up definition to remove call address.
		dec 	hl
		dec 	hl
		ld 		(CodeNextFree),hl 			; keeping the CALL opcode.
		;
		ld 		hl,ConstantHandler 			; make it CALL ConstantHandler
		call 	CompileWord
		ld 		hl,(RegA)					; and put the word in after.
		call 	CompileWord
		ret
@end		

; ***************************************************************************************
;
;							Word that defines a variable
;
; ***************************************************************************************

@executes variable
		ld 		hl,(CodeNextFree) 			; fix up definition to remove call address.
		dec 	hl
		dec 	hl
		ld 		(CodeNextFree),hl 			; keeping the CALL opcode.
		;
		ld 		hl,VariableHandler 			; make it CALL VariableHandler
		call 	CompileWord
		ld 		hl,$0000					; initialise to zero.
		call 	CompileWord
		ret
@end		

; ***************************************************************************************
;
;							Word that defines user compiled data
;
; ***************************************************************************************

@executes data
		ld 		hl,(CodeNextFree) 			; fix up definition to remove call address.
		dec 	hl
		dec 	hl
		ld 		(CodeNextFree),hl 			; keeping the CALL opcode.
		;
		ld 		hl,VariableHandler 			; make it CALL VariableHandler
		call 	CompileWord
		ret
@end		

; ***************************************************************************************
;
;							Word that defines an array of memory.
;
; ***************************************************************************************

@executes array
		ld 		hl,(CodeNextFree) 			; fix up definition to remove call address.
		dec 	hl
		dec 	hl
		ld 		(CodeNextFree),hl 			; keeping the CALL opcode.
		;
		ld 		hl,VariableHandler 			; make it CALL VariableHandler
		call 	CompileWord

		ld 		hl,(RegA)					; initialise to zero.
_MakeArray:
		ld 		a,h
		or 		l
		ret 	z
		dec 	hl
		xor 	a		
		call 	CompileByte
		jr 		_MakeArray
		ret
@end		

; ***************************************************************************************
;
;	  Gets word address - compile <word> address.of and it loads it as a constant
;
; ***************************************************************************************

@executes address.of
		push 	de
		push 	hl
		ld 		hl,(CodeNextFree) 			; get previous code address
		dec 	hl
		ld 		d,(hl)
		dec 	hl
		ld 		e,(hl)
		dec 	hl 
		ld 		(CodeNextFree),hl 			; we've removed the word, address is in DE
		ex 		de,hl
		call 	CompileLoadConstant
		pop 	hl
		pop 	de
		ret
@end

; ***************************************************************************************
;
; 									!! @@ overrides
;
; ***************************************************************************************

@executes !! compile
		ld 		hl,(CodeNextFree)	 		; we save one byte.
		dec 	hl
		ld 		(CodeNextFree),hl	
		;
		ld 		b,(hl)
		dec 	hl
		ld 		c,(hl)
		ld 		(hl),b
		dec 	hl
		ld 		(hl),c
		dec 	hl
		ld 		(hl),$22
		ret
@end

@executes @@ compile
		ld 		hl,(CodeNextFree)	 		; we save one byte.
		;
		dec 	hl
		dec 	hl
		dec 	hl
		ld 		(hl),$2A
		ret
@end

; ***************************************************************************************
;
; 						Called when the constant word is called.
;
; ***************************************************************************************

ConstantHandler:		
		ld 		a,0xEB 						; compile ex de,hl
		call 	CompileByte
		ld 		a,0x21 						; compile ld hl,
		call 	CompileByte
		pop 	hl 							; address of word to compile
		ld 		a,(hl)
		inc 	hl
		ld 		h,(hl)
		ld	 	l,a
		call 	CompileWord 				; compile that word.
		ret

; ***************************************************************************************
;
; 						Called when the variable word is called.
;
; ***************************************************************************************

VariableHandler:		
		ld 		a,0xEB 						; compile ex de,hl
		call 	CompileByte
		ld 		a,0x21 						; compile ld hl,
		call 	CompileByte
		pop 	hl 							; address of word to compile
		call 	CompileWord 				; compile that word.
		ret

; ***************************************************************************************
;
;		Fix Access code. Expecting  EX DE,HL LD HL,xxxxx replaces with LD/ST
;
; ***************************************************************************************

FixAccessCodeWrite:
		ld 		a,0x2A 						; LD HL,(xxxx)
		jr 		nc,_FACIsRead 				; use Carry to decide what to update
		ld 		a,0x22
_FACIsRead:
		;
		ld 		hl,(CodeNextFree)	 		; we save one byte.
		dec 	hl
		ld 		(CodeNextFree),hl	
		;
		ld 		b,(hl)
		dec 	hl
		ld 		c,(hl)
		ld 		(hl),b
		dec 	hl
		ld 		(hl),c
		dec 	hl
		ld 		(hl),a
		ret

